apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 29


    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.core:core-ktx:1.1.0"
    implementation "com.squareup.okhttp3:okhttp:4.2.1"

    // Kodein
    // implementation 'org.kodein.di:kodein-di-generic-jvm:5.2.0'
    // implementation 'org.kodein.di:kodein-di-framework-android-x:5.2.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.0.0'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        jcenter()

    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'

//        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
//        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

ext {
    bintrayRepo = 'Zyncy'
    bintrayName = 'Zyncy'

    publishedGroupId = 'com.zyncy'
    libraryName = 'Zyncy'
    artifact = 'zyncy'

    libraryDescription = 'This is a library is used for remote file(Images for the time being) ' +
            'sync to Android application and cache it in memory to prevent fetching the same ' +
            'again from remote for further requests.'

    siteUrl = 'https://github.com/sakkeerhussain/Zyncy'
    gitUrl = 'https://github.com/sakkeerhussain/Zyncy.git'

    libraryVersion = '0.2.8'

    developerId = 'sakkeerhussain'
    developerName = 'Sakkeer Hussain'
    developerEmail = 'sakkeerhussainp@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

group = publishedGroupId
version = libraryVersion

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId publishedGroupId
            artifactId artifact

            name libraryName
            description libraryDescription
            url siteUrl

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            developers {
                developer {
                    id developerId
                    name developerName
                    email developerEmail
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
            dependencyManagement {
                dependencies {
                    dependency {
                        groupId "com.squareup.okhttp3"
                        artifactId "okhttp"
                        version "4.2.1"
                    }
                    dependency {
                        groupId "androidx.appcompat"
                        artifactId "appcompat"
                        version "1.1.0"
                    }
                    dependency {
                        groupId "org.jetbrains.kotlin"
                        artifactId "kotlin-stdlib-jdk7"
                        version kotlin_version
                    }
                    dependency {
                        groupId "androidx.core"
                        artifactId "core-ktx"
                        version "1.1.0"
                    }
                }
            }

        }
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        dryRun = false
        publish = true
        override = false
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
        }
    }
}